import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
	id 'maven-publish'
	id 'java'
	id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
	id 'fabric-loom' version '0.7-SNAPSHOT' apply false
}

allprojects {
	//apply plugin: 'com.github.johnrengelman.shadow'
	apply plugin: 'fabric-loom'
	apply plugin: 'java-library'

	repositories {
		mavenCentral()
		jcenter()
		maven { url "https://maven.shedaniel.me" }
		maven { url "https://maven.terraformersmc.com/releases" }
		maven { url "https://jitpack.io" }
	}

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings "net.fabricmc:yarn:${rootProject.minecraft_version}+build.${rootProject.yarn_mappings}"
		modCompile "net.fabricmc:fabric-loader:${rootProject.loader_version}"
	}

	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
	}

	jar {
		from "LICENSE"
	}
}

subprojects {
	group = project.maven_group

	configurations {
		dev
	}

	jar {
		archiveBaseName.set(archivesBaseName)
		archiveClassifier.set("dev")
	}

	afterEvaluate {
		if (pluginManager.hasPlugin("com.github.johnrengelman.shadow")) {
			remapJar {
				dependsOn(shadowJar)
			}

			sourcesJar {
				dependsOn(shadowJar)
			}

			shadowJar {
				configurations = [project.configurations.shadow]
				archiveClassifier.set("dev")
			}
		}

		remapJar {
			archiveName = "${project.archivesBaseName}-${version}.jar"
			beforeEvaluate {
				input = file("${project.buildDir}/libs/$project.archivesBaseName-${version}-dev.jar")
			}
		}

		artifacts {
			dev file: file("${project.buildDir}/libs/$project.archivesBaseName-${version}-dev.jar"), type: "jar", builtBy: jar
		}

		processResources {
			inputs.property "version", project.version

			from(sourceSets.main.resources.srcDirs) {
				include "fabric.mod.json"
				expand "version": project.version
			}

			from(sourceSets.main.resources.srcDirs) {
				exclude "fabric.mod.json"
			}
		}
	}

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this task, sources will not be generated.
	task sourcesJar(type: Jar, dependsOn: classes) {
		archiveBaseName.set(archivesBaseName)
		archiveClassifier.set("sources")
		from sourceSets.main.allSource
	}

	// configure the maven publication
	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifactId = "${project.archivesBaseName}-${project.minecraft_major_version}"
				// add all the jars that should be included when publishing to maven
				artifact(remapJar) {
					builtBy remapJar
				}
				artifact(sourcesJar) {
					builtBy remapSourcesJar
				}
			}
		}

		// select the repositories you want to publish to
		repositories {
			if (project.hasProperty("siphalorMavenUser")) {
				maven {
					url = "https://maven.siphalor.de/upload.php/"
					name = "Siphalor"
					credentials {
						username = siphalorMavenUser
						password = siphalorMavenPassword
					}
				}
			}
			// uncomment to publish to the local maven
			// mavenLocal()
		}
	}

	task updateModMetaFiles(type: Copy) {
		def projectId = project.archivesBaseName.replace('-', '_')
		from rootProject.file(".template/icon.png")
		into "src/main/resources/assets/${projectId}"
		include "*.png"
		outputs.upToDateWhen { false }
		doLast {
			def slurper = new JsonSlurper()
			def template = slurper.parse(rootProject.file(".template/fabric.mod.json"))
			def path = file("src/main/resources/fabric.mod.json")
			def json = path.exists() ? slurper.parse(path) : new HashMap<>()
			json += template
			file("src/main/resources/fabric.mod.json")
					.write(JsonOutput.prettyPrint(JsonOutput.toJson(json)).replace("\${project}", projectId))
		}
	}
}

group = project.maven_group

dependencies {
	subprojects.each {
		if (it.configurations.getAsMap().containsKey("shadowDev")) {
			implementation(project(path: ":$it.name", configuration: "shadow")) {
				exclude module: "fabric-loader"
			}
		} else {
			implementation(project(path: ":$it.name", configuration: "dev")) {
				exclude module: "fabric-loader"
			}
		}

		include(project("${it.name}:"))

		modRuntime("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
			exclude module: "fabric-loader"
		}

	}
}
